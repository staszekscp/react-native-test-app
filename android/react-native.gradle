ext.rnta_react_native_gradle = true

if (!hasProperty("rnta_node_gradle")) {
    apply(from: "${buildscript.sourceFile.getParent()}/node.gradle")
}

ext.isBridgelessEnabled = { Project project, boolean isNewArchEnabled ->
    if (isNewArchEnabled) {
        def bridgelessEnabled = project.findProperty("react.bridgelessEnabled")
                                    ?: project.findProperty("bridgelessEnabled")

        def version = getPackageVersionNumber("react-native", project.rootDir)
        if (version >= v(0, 82, 0)) {
            if (bridgelessEnabled == "false") {
                logger.warn("WARNING: As of 0.82, bridgeless can no longer be disabled")
            }
            return true
        }

        return bridgelessEnabled != "false"
    }
    return false
}

ext.isFabricEnabled = { Project project ->
    return isNewArchitectureEnabled(project)
}

ext.isNewArchitectureEnabled = { Project project ->
    def newArchEnabled = project.findProperty("react.newArchEnabled")
                             ?: project.findProperty("newArchEnabled")

    def version = getPackageVersionNumber("react-native", project.rootDir)
    if (version >= v(0, 82, 0)) {
        if (newArchEnabled == "false") {
            logger.warn("WARNING: As of 0.82, New Architecture can no longer be disabled")
        }
        return true
    }

    return newArchEnabled == "true"
}
